{-# LANGUAGE QuasiQuotes #-}
{-# LANGUAGE LambdaCase #-}
{-# OPTIONS_GHC -Wall -fno-warn-tabs #-}

module MakeEnum where

import Data.List
import Data.Char
import Text.Nowdoc
import System.Environment

makeEnum :: String -> String -> String -> String -> IO ()
makeEnum hf hsnm cnm ext = do
	prg <- getProgName
	src <- readFile hf
	writeFile ("../src/Vulkan/" ++ hsnm ++ ".hsc") $
		header prg [] hsnm cnm ["Show", "Eq", "Storable"] ++
			intercalate ",\n" (map makeItem . takeDefinition cnm $ lines src) ++ " ]\n" ++
		case ext of "" -> ""; _ -> "\n" ++ ext ++ "\n"

makeEnum' :: String -> [String] -> String -> String -> [String] -> String -> IO ()
makeEnum' hf icds hsnm cnm drvs ext = do
	prg <- getProgName
	src <- readFile hf
	writeFile ("../src/Vulkan/" ++ hsnm ++ ".hsc") $
		header prg icds hsnm cnm drvs ++
			intercalate ",\n" (map makeItem . takeDefinition cnm $ lines src) ++ " ]\n" ++
		case ext of "" -> ""; _ -> "\n" ++ ext ++ "\n"

takeDefinition :: String -> [String] -> [String]
takeDefinition nm =
	map (head . words) . takeWhile (not . (== "} " ++ nm ++ ";")) . tail
		. dropWhile (not . (("typedef enum " ++ nm ++ " {") `isPrefixOf`))

makeItem :: String -> String
makeItem cnst = '\t' : nm ++ sp ++ cst
	where
	nm = "(\"" ++ concat (map capitalize . tail $ sep '_' cnst) ++ "\","
	cst = "#{const " ++ cnst ++ "})"
	sp = if 8 + length nm + 1 + length cst + 1 > 80 then "\n\t\t" else " "

sep :: Eq a => a -> [a] -> [[a]]
sep s xs = case d of [] -> [t]; _ : r -> t : sep s r
	where (t, d) = span (/= s) xs
	
capitalize :: String -> String
capitalize = \case
	"" -> ""
	c : cs -> toUpper c : map toLower cs

header :: String -> [String] -> String -> String -> [String] -> String
header tnm icds hsnm cnm drvs =
	"-- This file is automatically generated by the tools/" ++ tnm ++
	"\n--\t% stack runghc --cwd tools/ " ++ tnm ++
	[nowdoc|


{-# LANGUAGE TemplateHaskell #-}
{-# LANGUAGE PatternSynonyms #-}
{-# LANGUAGE GeneralizedNewtypeDeriving #-}
{-# OPTIONS_GHC -Wall -fno-warn-tabs #-}

module |] ++ "Vulkan." ++ hsnm ++ [nowdoc| where

import Foreign.Storable
import Foreign.C.Enum
import Data.Word
|] ++ unlines (("import " ++) <$> icds) ++ [nowdoc|

#include <vulkan/vulkan.h>

enum |] ++ "\"" ++ hsnm ++ "\" ''#{type " ++ cnm ++
	"}\n\t\t" ++ makeDrvs drvs ++ " [\n"

makeDrvs :: [String] -> String
makeDrvs = ('[' :) . (++ "]") . intercalate ", " . map ("''" ++)
